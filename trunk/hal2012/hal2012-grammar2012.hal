#
# This file is part of FreeHAL 2012.
#
# Copyright(c) 2006, 2007, 2008, 2009, 2010, 2011, 2012 Tobias Schulz and contributors.
# http://www.freehal.org
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 3
# of the License, or any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
#


compile source hal2012-defs.hal
require source hal2012-defs.hal

define action elements_of_array with array data, var i do
    set new array _data to an empty array

    for each new array __data in array data do
        push into array _data, from array __data element [ var i ]
    done

    do return with array _data
done

define action new_parser_start with array grammar, array data, var lang, var path do

    set new array data_words to elements_of_array with array data, 0
    set new array data_pos to elements_of_array with array data, 1

    set new var input to parser2012_make_input with array data_pos, array data_words, array data
    do hal2012_send_signal with "grammar2012", var input
    set new var to_eval to hal2012_fetch_signal with "grammar2012"

    set new hash parsed to an empty hash
    do eval with var to_eval
    if hash parsed is false do
        print "Error! hash parsed is false!"
        set hash parsed to fake_hash_parsed without arguments
    done

    do return with hash parsed
done

define action parser2012_make_input with array data_pos, array data_words, array data do
    set new var data_pos_str to ""
    for each new array data_element in array data do
        set new var symbol to from array data_element second element
        set new var word to from array data_element first element
        set var data_pos_str to var data_pos_str concat " " concat var symbol concat " < " concat var word concat " >"
    done
    set var data_pos_str to var data_pos_str concat " "
    print "parser2012_make_input: " concat var data_pos_str concat new line
    do return with var data_pos_str
done
